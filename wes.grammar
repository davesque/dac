NAME: [a-zA-Z_][a-zA-Z0-9_]*
VAL: (0b[01_]+|0o[0-7_]+|[0-9_]+|0x[a-fA-F0-9_]+)

file: stmt* EOF

stmt: offset | label | inst

offset: relative | absolute

relative: ('+' | '-') VAL ':' [NEWLINE]
absolute: VAL ':' [NEWLINE]

label: NAME ':' [NEWLINE]

inst: nullary | unary | binary

nullary: expr NEWLINE
unary: NAME arg NEWLINE
binary: NAME arg ',' arg NEWLINE

arg: deref | expr

deref: '[' expr ']'

expr: '(' inner ')' | inner
inner: un_expr | bin_expr | atom

un_expr: un_op expr
bin_expr: expr bin_op expr
atom: NAME | VAL

un_op: '-' | '~'
bin_op: '-' | '+' | '*' | '/' | '>>' | '<<' | '^' | '&' | '|' | '**' | '%'
