NAME: [a-zA-Z_][a-zA-Z0-9_]*
VAL: (0b[01_]+|0o[0-7_]+|[0-9_]+|0x[a-fA-F0-9_]+)

file: stmt* EOF

stmt: const | offset | label | inst

const: name '=' expr NEWLINE

offset: relative | absolute

relative: ('+' | '-') VAL ':' [NEWLINE]
absolute: VAL ':' [NEWLINE]

label: NAME ':' [NEWLINE]

inst: nullary | unary | binary

nullary: expr NEWLINE
unary: NAME expr NEWLINE
binary: NAME expr ',' expr NEWLINE

expr: expr '|' xor | xor
xor: xor '^' and | and
and: and '&' shift | shift
shift: shift ('<<' | '>>') sum | sum

sum: sum ('+' | '-') term | term
term: term ('*' | '/' | '%') factor | factor
factor: ('-' | '~') factor | power
power: atom '**' factor | atom

atom: '[' expr ']' | '(' expr ')' | NAME | VAL
